@ tag load
schedule function / 10t

var particle_dist = warp_config.particle_view_distance

as @e[type=$wp_indicator]
at @s
unless entity @e[type=$wp_pad,
    tag=wp.active,
    distance=..2.9]
    kill @s

# set player UUID score
set @a->wp.linked_UUID += 0
as @a[scores={wp.linked_UUID=0}]
    set @s->wp.linked_UUID = @s.UUID[0]

# set wp freq score from data
as @e[type=$wp_pad]
    set @s->wp.frequency = @s.data.Frequency (int)

as @e[type=$wp_pad]
set @s->wp.linked_UUID = @s.data.Owner_UUID[0]

for (var i=0; i<warp_config.tiers.length; i++) {
    
    var tier = warp_config.tiers[i]
    
    as @e[type=item,
        tag=!global.ignore,tag=!global.ignore.kill,
        nbt={
            Item:{
                id:${(string) tier.primary}
            }
        }] 
    at @s
    align xyz
    if entity @e[type=item,
        tag=!global.ignore,tag=!global.ignore.kill,
        dx=0,dy=0,dz=0,
        nbt={
            Item:{
                id:${(string) tier.secondary},
                Count:4b
            }
        }]
    positioned ~0.5 ~0.5 ~0.5
    unless entity @e[type=$wp_pad,distance=..2.9]
    if block ~ ~-1 ~ ${tier.base}
    if entity @p[distance=..32]
    function { # Create warp pads
        
        # ^0.2 is the magic number to center these later!
        
        summon $wp_anim_stand ~ ~-1.4 ~ {
            Tags:["wp.primary_anim"],
            ArmorItems:[
                {},
                {},
                {},
                {
                    id:${(string) tier.primary},
                    Count:1b
                }
            ],
            Rotation:[
                0.0f,
                0.0f
            ]
        }
        
        for (var i=0; i<4; i++) {
            var rot = nbt_value<[${(tag_float) (i * 90 + 45)}, -25.0f]> # Get rotation, second number will influence height increase in spawn animation
            
            summon $wp_anim_stand ~ ~-1.4 ~ {
                Tags:[
                    "wp.secondary_anim",
                    ${ "wp.secondary_anim_" + i }
                ],
                ArmorItems:[
                    {},
                    {},
                    {},
                    {
                        id:${(string) tier.secondary},
                        Count:1b
                    }
                ],
                Rotation:$rot,
                Pose:{
                    Head:[
                        -15.0f,
                        0.0f,
                        0.0f
                    ]
                }
            }
        }
        
        as @e[tag=wp.primary_anim]
        at @s
        function {
            facing entity @p feet
            tp @s ^ ^ ^0.2 ~ 0
            as @e[tag=wp.secondary_anim,distance=..2]
            at @s
            tp @s ^ ^ ^0.2
        }
        
        particle witch ~ ~-0.2 ~ 0.25 0 0.25 1 80 normal @a[distance=..$particle_dist]
        
        summon $wp_pad ~ ~ ~ {
            Tags:["wp.temp"]
        }
        set @e[tag=wp.temp]->wp.spawn_anim = 100
        # cheap workaround for component @s add ${"tier_" + i}
        tag @e[tag=wp.temp] add ${"trident-component.smoochypit.tier_" + i}
        as @e[tag=wp.temp]
            schedule function smoochypit:warppad/entities/warppad/wp_pad/animate_per 1t
        using tag wp.mainitem @s {
            as @e[tag=wp.temp]
            at @s
                function smoochypit:warppad/entities/warppad/wp_pad/construct
        }
        tag @e remove wp.temp
        
        align xyz
        kill @e[type=item,
            tag=!global.ignore,tag=!global.ignore.kill,
            dx=0,dy=0,dz=0,
            nbt={
                Item:{
                    id:${(string) tier.secondary},
                    Count:4b
                }
            }]
        
        kill @s
        
    }
    
    as @e[
        type=$wp_pad,
        tag=${"trident-component.smoochypit.tier_" + i},
        nbt={data:{Destructable:true}}
    ]
    at @s
    unless block ~ ~-1 ~ ${tier.base}
    if entity @p[distance=..32]
    function smoochypit:warppad/entities/warppad/wp_pad/destruct
    
    #no player in 32 block comparison here, as no chunk forceloads are being modified
    as @e[
        type=$wp_pad,
        tag=${"trident-component.smoochypit.tier_" + i},
        nbt={data:{Destructable:false}}
    ]
    at @s
    unless block ~ ~-1 ~ ${tier.base}
    function smoochypit:warppad/entities/warppad/wp_pad/no_break
    
}
    
as @e[type=$wp_pad]
at @s
    function /../modification
