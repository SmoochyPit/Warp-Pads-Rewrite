@ on compile
@ require smoochypit:warppad/entities/animation

var particle_dist = warp_config.particle_view_distance

define objective wp.temp_ID
define objective wp.player_launch minecraft.custom:minecraft.sneak_time {"text":"Warp Launch"}
define objective wp.cooldown
# define objective wp.spamcount ( Moved to entities/warppad )

define entity wp_indicator marker {
    
    default name [
        
        "Warp Pad Icon Indicator"
        
    ]
    
    default nbt {
        
        CustomNameVisible:false,
        data:{
            Color:"default",
            Home_UUID:[
                I;
                0,
                0,
                0,
                0
            ],
            Associated_UUID:[
                I;
                0,
                0,
                0,
                0
            ]
        },
        Tags:[
            "wp.warp_icon"
        ]
    }
    
}

define function /icon_align {
    
    # as and at all active warps not yet processed
    
    as @e[
        tag=wp.valid_dest
    ]
    function {
        
        positioned ~ ~1.14 ~
        facing entity @s
            summon $wp_indicator{Tags:["wp.temp"]} ^ ^ ^${warp_config.icon.distance_away}
        
        align xyz
            set @e[tag=wp.temp,limit=1].data.Home_UUID = @e[type=$wp_pad,dx=0,dy=0,dz=0,limit=1].UUID
        
        set @e[tag=wp.temp,limit=1].data.Associated_UUID = @s.UUID
        set @e[tag=wp.temp,limit=1].CustomName = @s.CustomName
        set @e[tag=wp.temp,limit=1].data.Color = @s.data.Color
        set @e[tag=wp.temp,limit=1]->wp.x = @s->wp.x
        set @e[tag=wp.temp,limit=1]->wp.z = @s->wp.z
        
        if @s[scores={wp.temp_ID=1..}]
            set @e[tag=wp.temp,limit=1]->wp.temp_ID = @s->wp.temp_ID
        set @e[type=$wp_pad]->wp.temp_ID += 0
        if @s[scores={wp.temp_ID=0}]
        function {
            set @e[scores={wp.temp_ID=1..}]->wp.temp_ID += 1
            set @s->wp.temp_ID = 1
            set @e[tag=wp.temp,limit=1]->wp.temp_ID = 1
        }
        
        tag @e remove wp.temp
        
    }
    
}

define function /tick_cool_down {
    
    # Decrement cooldown and spamcount
    # If spamcount is >= 100, tag as wp.spammed, set cooldown and reset spamcount
    # Remove tag wp.spammed from anybody with no cooldown
    
    set @a[scores={
            wp.cooldown=1..
        }]->wp.cooldown -= 1
    set @a[scores={
            wp.spamcount=1..
        }]->wp.spamcount -= 2
    
    as @a[scores={wp.spamcount=100..}]
    function {
        tag @s add wp.spammed
        set @s->wp.cooldown = ${warp_config.spam_punishment * 4}
        set @s->wp.spamcount = 0
    }
    
    tag @a[scores={wp.cooldown=0}] remove wp.spammed
    
    schedule clear /
    
    if entity @a[scores={
            wp.cooldown=1..
        }]
    schedule function / 5t
    
    if entity @a[scores={
            wp.spamcount=1..
        }]
    schedule function / 5t
    
}

define function /make_selector {
    
    # as and at all active warps not yet processed
    
    summon marker{Tags:[
            "wp.selector",
            "wp.temp"
        ]} ~ ~1.14 ~
    
}

define function /mark_selected {
    
    # as $wp_indicator
    # at current active $wp_pad
    
    tag @s add wp.selected_icon
    
    at @s
    unless entity @s[nbt={
            CustomName:'{"text":"Warp Pad"}'
        }]
    unless entity @e[tag=wp.name_display,distance=..0.001]
    function {
        summon $wp_anim_stand ~ ~ ~ {Tags:["wp.name_display"],CustomNameVisible:false}
        set @e[tag=wp.name_display,distance=..0.001,limit=1,sort=nearest].CustomName = @s.CustomName
        data modify entity @e[tag=wp.name_display,distance=..0.001,limit=1,sort=nearest] CustomNameVisible set value true
    }
    
    at @e[tag=wp.selecting,limit=1]
    align xyz
    function {
        
        # Tag removed in logic.tdn
        tag @p[tag=!global.ignore.gui,tag=wp.pilot,dx=0,dy=0,dz=0] add wp.displaying_warp_gui
        tag @a[tag=wp.displaying_warp_gui] add global.ignore.gui
        title @p[tag=wp.displaying_warp_gui,dx=0,dy=0,dz=0] actionbar {
            "text":"Destination: ",
            "color":"gold",
            "extra":[
                {
                    "selector":"@s",
                    "color":"light_purple"
                },
                {
                    "text":" at "
                },
                {
                    "score":{
                        "name":"@s",
                        "objective":"wp.x"
                    },
                    "color":"red"
                },
                {
                    "text":" "
                },
                {
                    "score":{
                        "name":"@s",
                        "objective":"wp.z"
                    },
                    "color":"blue"
                }
            ]
        }
        
    }
    
}


define function /selection {
    
    # as and at all processed, active pads
    
    positioned ~ ~1.14 ~
        tag @e[type=$wp_indicator,distance=..${warp_config.icon.distance_away + 0.1}] remove wp.selected_icon
    
    using tag wp.selecting @s {
    
        align xyz
        at @p[tag=wp.pilot,dx=0,dy=0,dz=0]
        positioned ~ ~1.64 ~
        as @e[tag=wp.selector,limit=1,sort=nearest]
        function {
            
            tp @s ^ ^ ^${warp_config.icon.distance_away} ~ ~
            
            if (warp_config.icon.selection_particle.size != 0.0) {
                
                var particle = warp_config.icon.selection_particle
                
                at @s
                    particle dust ${particle.r} ${particle.g} ${particle.b} ${particle.size} 
                    ^ ^ ^-0.1 0 0 0 ${particle.speed} 1 normal @a[distance=..$particle_dist]
                
            }
            
            at @s
            as @e[type=$wp_indicator,
                distance=..${warp_config.icon.selection_radius},
                limit=1,
                sort=nearest]
            function /../../mark_selected
                
            tp @s ^ ^ ^ ~ ~
            
        }
        
    }
    
    positioned ~ ~1.14 ~
    unless entity @e[tag=wp.selected_icon,distance=..${warp_config.icon.distance_away + 0.1}]
        title @a[tag=wp.displaying_warp_gui] actionbar ""
    
}

define function /teleport {
    
    tp @s ~ ~-0.5 ~
    
    unless entity @a[scores={
            wp.cooldown=1..
        }]
    schedule function smoochypit:warppad/logic/warping/tick_cool_down 5t
    
    set @s->wp.cooldown = 3
    set @s->wp.spamcount += 1
    
    set @s->wp.player_launch = 0
    
}

define function /warp {
    
    # at and as pads with a pilot sneaking
    
    tag @e[type=$wp_pad] remove wp.dest_pad
    
    positioned ~ ~1.14 ~
    as @e[
        tag=wp.selected_icon,
        distance=..${warp_config.icon.distance_away + 0.1},
        limit=1,
        sort=nearest
    ]
    as @e[
        type=$wp_pad,
        scores={
            wp.temp_ID=1..
        }
    ]
    if score @s wp.temp_ID = @e[
        tag=wp.selected_icon,
        distance=..${warp_config.icon.distance_away + 0.1},
        limit=1,
        sort=nearest
        ] wp.temp_ID
    tag @s add wp.dest_pad
    
    function smoochypit:warppad/entities/warppad/wp_pad/inc_spam
    
    align xyz
    if entity @e[tag=wp.dest_pad]
    as @e[tag=wp.pilot,dx=0,dy=0,dz=0]
    at @e[tag=wp.dest_pad,limit=1]
    function /../teleport
    
}
