@ on compile
@ require warppad:entities/animation
@ require warppad:logic/passengers

var particle_dist = warp_config.particle_view_distance

define objective wp.temp_ID
define objective wp.pass_count
define objective wp.player_launch minecraft.custom:minecraft.sneak_time {"text":"Warp Launch"}
define objective wp.cooldown
# define objective wp.spamcount ( Moved to entities/warppad )

define entity wp_indicator marker {
    
    default name [
        
        "Warp Pad Icon Indicator"
        
    ]
    
    default nbt {
        
        CustomNameVisible:false,
        data:{
            Color:"default",
            Home_UUID:[
                I;
                0,
                0,
                0,
                0
            ],
            Associated_UUID:[
                I;
                0,
                0,
                0,
                0
            ]
        },
        Tags:[
            "wp.warp_icon"
        ]
    }
    
}

define function /icon_align {
    
    # as and at all active warps not yet processed
    
    as @e[
        tag=wp.valid_dest
    ]
    function {
        
        summon $wp_indicator{Tags:["wp.temp"]} ~ ~ ~
        
        positioned ~ ~1.14 ~
        facing entity @s
            tp @e[tag=wp.temp] ^ ^ ^${warp_config.icon.distance_away} facing entity @s
        
        align xyz
            set @e[tag=wp.temp,limit=1].data.Home_UUID = @e[type=$wp_pad,dx=0,dy=0,dz=0,limit=1].UUID
        
        set @e[tag=wp.temp,limit=1].data.Associated_UUID = @s.UUID
        set @e[tag=wp.temp,limit=1].CustomName = @s.CustomName
        set @e[tag=wp.temp,limit=1].data.Color = @s.data.Color
        set @e[tag=wp.temp,limit=1]->wp.x = @s->wp.x
        set @e[tag=wp.temp,limit=1]->wp.z = @s->wp.z
        
        if @s[scores={wp.temp_ID=1..}]
            set @e[tag=wp.temp,limit=1]->wp.temp_ID = @s->wp.temp_ID
        set @e[type=$wp_pad]->wp.temp_ID += 0
        if @s[scores={wp.temp_ID=0}]
        function {
            set @e[scores={wp.temp_ID=1..}]->wp.temp_ID += 1
            set @s->wp.temp_ID = 1
            set @e[tag=wp.temp,limit=1]->wp.temp_ID = 1
        }
        
        tag @e remove wp.temp
        
    }
    
}

define function /tick_cool_down {
    
    # Decrement cooldown and spamcount
    # If spamcount is >= 100, tag as wp.spammed, set cooldown and reset spamcount
    # Remove tag wp.spammed from anybody with no cooldown
    
    set @a[scores={
            wp.cooldown=1..
        }]->wp.cooldown -= 1
    set @a[scores={
            wp.spamcount=1..
        }]->wp.spamcount -= 2
    
    as @a[scores={wp.spamcount=100..}]
    function {
        tag @s add wp.spammed
        advancement grant @s only warppad:actions/spam
        set @s->wp.cooldown = ${warp_config.spam_punishment * 4}
        set @s->wp.spamcount = 0
    }
    
    tag @a[scores={wp.cooldown=0}] remove wp.spammed
    
    schedule clear /
    
    if entity @a[scores={
            wp.cooldown=1..
        }]
    schedule function / 5t
    
    if entity @a[scores={
            wp.spamcount=1..
        }]
    schedule function / 5t
    
}

define function /surprise_players {

    @ tag load

    # This function will run intermittently to check for players with a cooldown that have joined since the last run
    
    schedule clear /
    schedule function / 5s

    schedule clear /../tick_cool_down
    
    if entity @a[scores={
            wp.cooldown=1..
        }]
    schedule function /../tick_cool_down 5t
    
    if entity @a[scores={
            wp.spamcount=1..
        }]
    schedule function /../tick_cool_down 5t

}

define function /make_selector {
    
    # as and at all active warps not yet processed
    
    summon marker{Tags:[
            "wp.selector"
        ]} ~ ~1.14 ~
    
}

define function /mark_selected {
    
    # as $wp_indicator
    # at current active $wp_pad
    
    tag @s add wp.selected_icon
    
    at @s
    unless entity @s[nbt={
            CustomName:'{"text":"Warp Pad","italic":false}'
        }]
    unless entity @e[tag=wp.name_display,distance=..0.001]
    if block ~ ~ ~ #warppad:icon_no_coll
    function {
        summon $wp_anim_stand ~ ~ ~ {Tags:["wp.name_display"],CustomNameVisible:false}
        set @e[tag=wp.name_display,distance=..0.001,limit=1,sort=nearest].CustomName = @s.CustomName
        data modify entity @e[tag=wp.name_display,distance=..0.001,limit=1,sort=nearest] CustomNameVisible set value true
    }
    
    at @e[tag=wp.selecting,limit=1]
    align xyz
    function {
        
        # Tag removed in logic.tdn
        tag @p[tag=!global.ignore.gui,tag=wp.pilot,dx=0,dy=0,dz=0] add wp.displaying_warp_gui
        tag @a[tag=wp.displaying_warp_gui] add global.ignore.gui
        title @p[tag=wp.pilot,tag=wp.displaying_warp_gui,dx=0,dy=0,dz=0] actionbar {
            "text":"Destination: ",
            "color":"gold",
            "extra":[
                {
                    "selector":"@s",
                    "color":"light_purple"
                },
                {
                    "text":" at "
                },
                {
                    "score":{
                        "name":"@s",
                        "objective":"wp.x"
                    },
                    "color":"red"
                },
                {
                    "text":" "
                },
                {
                    "score":{
                        "name":"@s",
                        "objective":"wp.z"
                    },
                    "color":"blue"
                }
            ]
        }
        
    }
    
}


define function /selection {
    
    # as and at all processed, active pads
    
    positioned ~ ~1.14 ~
        tag @e[type=$wp_indicator,distance=..${warp_config.icon.distance_away + 0.1}] remove wp.selected_icon
    
    using tag wp.selecting @s {
    
        align xyz
        at @p[tag=wp.pilot,dx=0,dy=0,dz=0]
        positioned ~ ~1.64 ~
        as @e[tag=wp.selector,limit=1,sort=nearest]
        function {
            
            tp @s ^ ^ ^${warp_config.icon.distance_away} ~ ~
            
            if (warp_config.icon.selection_particle.size != 0.0) {
                
                var particle = warp_config.icon.selection_particle
                
                at @s
                    particle dust ${particle.r} ${particle.g} ${particle.b} ${particle.size} 
                    ^ ^ ^-0.1 0 0 0 ${particle.speed} 1 normal @a[distance=..$particle_dist]
                
            }
            
            at @s
            as @e[type=$wp_indicator,
                distance=..${warp_config.icon.selection_radius},
                limit=1,
                sort=nearest]
            function /../../mark_selected
                
            tp @s ^ ^ ^ ~ ~
            
        }
        
    }
    
    positioned ~ ~1.14 ~
    unless entity @e[tag=wp.selected_icon,distance=..${warp_config.icon.distance_away + 0.1}]
        title @a[tag=wp.pilot,tag=wp.displaying_warp_gui] actionbar ""
    
}

define function /teleport {
    
    tp @s ~ ~-0.5 ~
    tp @e[tag=wp.passenger] ~ ~-0.5 ~
    as @e[tag=wp.passenger]
        function warppad:logic/passengers/set_offset

    advancement grant @s only warppad:actions/warp
    at @a[tag=wp.passenger]
        set @s->wp.pass_count += 1
	advancement grant @s[scores={wp.pass_count=3..}] only warppad:challenges/group
    set @a->wp.pass_count = 0
    if entity @e[tag=wp.passenger,type=wolf]
        advancement grant @s only warppad:actions/dog
    if entity @e[tag=wp.passenger,type=creeper]
        advancement grant @s only warppad:challenges/creep
    if entity @a[tag=wp.passenger]
    if entity @e[tag=wp.dest_pad,nbt={data:{Private:true}}]
        advancement grant @s only warppad:actions/privpass
    advancement grant @a[tag=wp.passenger] only warppad:actions/passenger

    
    unless entity @a[scores={
            wp.cooldown=1..
        }]
    schedule function warppad:logic/warping/tick_cool_down 5t
    
    set @s->wp.cooldown = 3
    set @s->wp.spamcount += 1
    
    set @s->wp.player_launch = 0
    
}

define function /warp {
    
    # at and as pads with a pilot sneaking
    
    positioned ~ ~1.14 ~
    as @e[
        tag=wp.selected_icon,
        distance=..${warp_config.icon.distance_away + 0.1},
        limit=1,
        sort=nearest
    ]
    as @e[
        type=$wp_pad,
        scores={
            wp.temp_ID=1..
        }
    ]
    if score @s wp.temp_ID = @e[
        tag=wp.selected_icon,
        distance=..${warp_config.icon.distance_away + 0.1},
        limit=1,
        sort=nearest
        ] wp.temp_ID
    tag @s add wp.dest_pad
    
    if entity @e[tag=wp.dest_pad]
    function warppad:entities/warppad/wp_pad/inc_spam

    if entity @e[tag=wp.dest_pad]
    align y
    as @e[
        tag=!wp.pilot,
        type=!#warppad:nonwarpable,
        tag=!global.ignore,tag=!global.ignore.pos,
        tag=!smithed.strict,tag=!smithed.block,tag=!wp.nowarp,
        distance=..${warp_config.warp_circle.radius}
        ]
    positioned ~${-1 * warp_config.warp_circle.radius} ~ ~${-1 * warp_config.warp_circle.radius}
    tag @s[
        dx=${2 * warp_config.warp_circle.radius},
        dy=0,
        dz=${2 * warp_config.warp_circle.radius}
        ] add wp.passenger
        
    as @e[tag=wp.passenger]
    function warppad:logic/passengers/get_offset

    if entity @e[tag=wp.dest_pad]
    align xyz
    as @p[tag=wp.pilot,dx=0,dy=0,dz=0]
    at @e[tag=wp.dest_pad,limit=1]
    function /../teleport

    align xyz
    if entity @e[component=tier_2,dx=0,dy=0,dz=0]
    as @a[tag=wp.pilot,nbt={SelectedItem:{id:"minecraft:poppy"}},distance=50000..]
    advancement grant @s only warppad:challenges/warp50000

    tag @e remove wp.passenger
    tag @e remove wp.dest_pad
    
}
